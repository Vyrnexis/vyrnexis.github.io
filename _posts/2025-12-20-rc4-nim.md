---
layout: default
title: "RC4 in Nim — Encrypt/Decrypt"
date: 2025-12-20
author: "Vyrnexis"
---

Here’s a small, self-contained RC4 implementation in Nim with hex output for the ciphertext and a simple self-test.

```nim
##[
 RC4 Encryption/Decryption in Nim
 This code implements the RC4 stream cipher algorithm for encryption and decryption.
 It includes functions to initialize the key, encrypt plaintext, and decrypt ciphertext.
 The code is designed to be simple and easy to understand, with clear variable names and comments.
]##
import std/strutils

proc rc4KeyInit(key: string): array[0 .. 255, int] =
  var
    j = 0
    s: array[0 .. 255, int]
  for i in 0 .. 255:
    s[i] = i
  for i in 0 .. 255:
    j = (j + s[i] + ord(key[i mod key.len])) mod 256
    swap(s[i], s[j])
  result = s

proc rc4CryptDecrypt*(key, text: string, encrypt: bool): string =
  if key.len == 0:
    raise newException(ValueError, "Key cannot be empty")

  var
    i = 0
    j = 0
    s = rc4KeyInit(key)
    res = newStringOfCap(text.len)
    y = 0

  if encrypt:
    for c in text:
      i = (i + 1) mod 256
      j = (j + s[i]) mod 256
      swap(s[i], s[j])
      let k = s[(s[i] + s[j]) mod 256]
      res.add toHex(ord(c) xor k, 2)
  else:
    if text.len mod 2 != 0 or text.len == 0:
      raise newException(
        ValueError,
        "Encrypted text must be a non-empty hexadecimal string with an even length",
      )
    while y < text.len:
      i = (i + 1) mod 256
      j = (j + s[i]) mod 256
      swap(s[i], s[j])
      let k = s[(s[i] + s[j]) mod 256]
      let byteVal = fromHex[int](text[y .. y + 1]) xor k
      res.add char(byteVal)
      inc(y, 2)
  result = res

# Unit tests
when isMainModule:
  block:
    let
      key = "MySecretKey"
      plaintext = "Hello, World!"
      ciphertext = rc4CryptDecrypt(key, plaintext, true)
      decrypted = rc4CryptDecrypt(key, ciphertext, false)
    doAssert decrypted == plaintext, "Encryption/Decryption failed"
    echo "Encryption/Decryption successful!"
    echo "Key: " & key
    echo "String: " & plaintext
    echo "RC4 Encrypted String: " & ciphertext
    echo ""
    echo "Decrypting: " & ciphertext
    echo "Key: " & key
    echo "Decrypted Plaion text: " & decrypted
```
